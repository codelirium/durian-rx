buildscript {
	repositories {
		jcenter()
		mavenLocal()
	}
	dependencies {
		// builds canonical OSGI bundles
		classpath "org.dm.gradle:gradle-bundle-plugin:${VER_BUNDLE}"
		// applies the Eclipse java formatter
		classpath "com.diffplug.gradle.spotless:spotless:${VER_SPOTLESS}"
		// uploads artifacts to jcenter
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${VER_BINTRAY}"
		configurations.all {
			resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
		}
	}
}

repositories {
	jcenter()
	mavenLocal()
} 

////////////
// GRADLE //
////////////
task wrapper(type: Wrapper) {
	gradleVersion VER_GRADLE
}

//////////
// JAVA //
//////////
apply plugin: 'java'
sourceSets {
	main { java {
			srcDir 'src'
	} }
	test { java {
			srcDir 'test'
	} }
}
sourceCompatibility = VER_JAVA
targetCompatibility = VER_JAVA

dependencies {
	compile "com.diffplug.durian:durian:${VER_DURIAN}"
	compile "com.google.guava:guava:${VER_GUAVA}"
	compile "io.reactivex:rxjava:${VER_RXJAVA}"
	testCompile "junit:junit:${VER_JUNIT}"
}

//////////
// OSGI //
//////////
apply plugin: 'org.dm.bundle'
bundle {
	instruction 'Bundle-Vendor', 'DiffPlug'
	instruction 'Bundle-Description', "${description}"
	instruction 'Bundle-DocURL', "https://github.com/${org}/${name}"
	instruction 'Bundle-License', 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.8'
	instruction 'Export-Package', 'com.diffplug.common.*'
	instruction '-removeheaders', 'VER_*'
}

// make sure we know what's going on with our OSGI manifest
task copyManifest(type: Sync, dependsOn: jar) {
	from 'build/tmp/jar'
	into 'META-INF'
	include 'MANIFEST.MF'
}
build.dependsOn(copyManifest)

/////////////
// ECLIPSE //
/////////////
apply plugin: 'eclipse'
eclipse {
	project {
		natures 'org.eclipse.pde.PluginNature'
		natures 'org.eclipse.jdt.core.javanature'

		buildCommand 'org.eclipse.jdt.core.javabuilder'
		buildCommand 'org.eclipse.pde.ManifestBuilder'
		buildCommand 'org.eclipse.pde.SchemaBuilder'

		// filter out the build folder
		file {
			withXml { xmlProvider ->
				Node project = xmlProvider.asNode()
				Node filter = project.appendNode('filteredResources').appendNode('filter')
				filter.appendNode('id', project.name.hashCode()) 	// use the project name's hash as the filter ID
				filter.appendNode('name', '')
				filter.appendNode('type', 10)
				Node matcher = filter.appendNode('matcher')
				matcher.appendNode('id', 'org.eclipse.ui.ide.multiFilter')
				matcher.appendNode('arguments', '1.0-name-matches-false-false-build')
			}
		}
	}
	classpath {
		downloadSources true
		downloadJavadoc true
	}
	jdt {
		sourceCompatibility VER_JAVA
		targetCompatibility VER_JAVA
	}
}
// always create fresh projects
tasks.eclipse.dependsOn(cleanEclipse)

////////////
// FORMAT //
////////////
apply plugin: 'com.diffplug.gradle.spotless'
spotless {
	licenseHeaderFile = file('spotless.license.java')			// License header file
	importsOrderFile = file('spotless.importorder.properties')	// An import ordering file, exported from Eclipse
	eclipseFormatFile = file('spotless.eclipseformat.xml')		// XML file dumped out by the Eclipse formatter
}

//////////////
// FINDBUGS //
//////////////
apply plugin: 'findbugs'
findbugs {
	toolVersion = VER_FINDBUGS
	sourceSets = [sourceSets.main]	// don't check the test code
	ignoreFailures = false 	// bug free or it doesn't ship!
	reportsDir = file('build/findbugs')
	effort = 'max'			// min|default|max
	reportLevel = 'low'		// low|medium|high (low = sensitive to even minor mistakes)
	omitVisitors = []		// bugs that we want to ignore
}
// HTML instead of XML
tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}
// we'll want the findbugs annotations
dependencies {
	compile "com.google.code.findbugs:annotations:${VER_FINDBUGS}"
}

///////////
// MAVEN //
///////////
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

// Where it's possible to name parameters and methods clearly enough
// that javadoc is not necessary, why make the code bigger?
//
// Thus, no javadoc warnings.
javadoc {
	options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
	if (project.hasProperty('dp_maven')) {
		repositories {
			maven {
				if (project.version.endsWith('-SNAPSHOT')) {
					url "${dp_maven}/libs-snapshot-local"
				} else {
					url "${dp_maven}/libs-release-local"
				}

				credentials {
					username "${dp_maven_user}"
					password "${dp_maven_pass}"
				}
			}
		}
	}
}

/////////////
// BINTRAY //
/////////////
if (project.hasProperty('dp_bintray_user')) {
	apply plugin: 'com.jfrog.bintray'
	bintray {
		user = dp_bintray_user
		key = dp_bintray_key

		publish=false
		publications = ['mavenJava']
		pkg {
			repo = 'opensource'
			name = project.name
			userOrg = project.org
			desc = project.description
			websiteUrl = "https://github.com/${org}/${name}"
			issueTrackerUrl = "https://github.com/${org}/${name}/issues"
			vcsUrl = "https://github.com/${org}/${name}"
			licenses = ['Apache-2.0']
			publicDownloadNumbers = true
			// Optional version descriptor
			version {
				name = project.version
				if (project.version.endsWith('SNAPSHOT')) {
					vcsTag = 'develop'
				} else {
					vcsTag = 'release/' + project.version
				}
			}
		}
	}
	// surprisingly, this actually needs to be here
	bintrayUpload.dependsOn(publish)
}
